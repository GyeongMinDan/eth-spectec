;;
;; Cursor
;;

syntax cursor = GLOBAL | BLOCK | LOCAL

var p : cursor

;;
;; Layered context
;;

;; Global layer

syntax globalTypingLayer =
  { CDENV cdenv,
    TDENV tdenv,
    RDENV rdenv,
    FRAME frame }

;; Block layer

syntax blockKind =
  | `EMPTY
  | EXTERN
  | PARSER
  | CONTROL
  | PACKAGE

syntax blockTypingLayer =
  { ID id,
    KIND blockKind,
    TDENV tdenv,
    RDENV rdenv,
    FRAME frame }

;; Local layer

syntax localKind =
  | `EMPTY
  | FUNCTION `-> typeIR
  | EXTERN_FUNCTION `-> typeIR
  | ACTION
  | EXTERN_METHOD `-> typeIR
  | EXTERN_METHOD ABSTRACT `-> typeIR
  | PARSER_STATE
  | CONTROL_APPLY_METHOD
  | TABLE_APPLY_METHOD

dec $is_function_localKind(localKind) : bool
def $is_function_localKind(FUNCTION `-> _) = true
def $is_function_localKind(_) = false
  -- otherwise

dec $is_extern_function_localKind(localKind) : bool
def $is_extern_function_localKind(
    EXTERN_FUNCTION `-> _
  ) = true
def $is_extern_function_localKind(_) = false
  -- otherwise

dec $is_action_localKind(localKind) : bool
def $is_action_localKind(ACTION) = true
def $is_action_localKind(_) = false
  -- otherwise

dec $is_extern_method_localKind(localKind) : bool
def $is_extern_method_localKind(
    EXTERN_METHOD `-> _
  ) = true
def $is_extern_method_localKind(_) = false
  -- otherwise

dec $is_extern_abstract_method_localKind(localKind) : bool
def $is_extern_abstract_method_localKind(
    EXTERN_METHOD ABSTRACT `-> _
  ) = true
def $is_extern_abstract_method_localKind(_) = false
  -- otherwise

dec $is_parser_state_localKind(localKind) : bool
def $is_parser_state_localKind(PARSER_STATE) = true
def $is_parser_state_localKind(_) = false
  -- otherwise

dec $is_control_apply_method_localKind(localKind) : bool
def $is_control_apply_method_localKind(
    CONTROL_APPLY_METHOD
  ) = true
def $is_control_apply_method_localKind(_) = false
  -- otherwise

dec $is_table_apply_method_localKind(localKind) : bool
def $is_table_apply_method_localKind(
    TABLE_APPLY_METHOD
  ) = true
def $is_table_apply_method_localKind(_) = false
  -- otherwise

syntax localTypingLayer =
  { ID id,
    KIND localKind,
    TDENV tdenv,
    FRAMES frame* }

;;
;; Typing context
;;

syntax context =
  { GLOBAL globalTypingLayer,
    BLOCK blockTypingLayer,
    LOCAL localTypingLayer }

var C : context

;;
;; Constructor
;;

dec $empty_context : context

def $empty_context = C
  -- if globalTypingLayer
      = { CDENV $empty_cdenv,
          TDENV $empty_tdenv,
          RDENV $empty_rdenv,
          FRAME $empty_frame }
  -- if blockTypingLayer
      = { ID "",
          KIND `EMPTY,
          TDENV $empty_tdenv,
          RDENV $empty_rdenv,
          FRAME $empty_frame }
  -- if localTypingLayer
      = { ID "",
          KIND `EMPTY,
          TDENV $empty_tdenv,
          FRAMES ([ $empty_frame ]) }
  -- if C
      = { GLOBAL globalTypingLayer,
          BLOCK blockTypingLayer,
          LOCAL localTypingLayer }

;;
;; Bound type variabls
;;

dec $bound(cursor, context) : bound

def $bound(GLOBAL, C) = `{ eps }
def $bound(BLOCK, C) = $dom_map<tid, typeDefIR>(C.BLOCK.TDENV)
def $bound(LOCAL, C) = $union_set<tid>(bound_block, bound_local)
  -- if bound_block = $bound(BLOCK, C)
  -- if bound_local = $dom_map<tid, typeDefIR>(C.LOCAL.TDENV)

;;
;; Frame entry and exit
;;

dec $enter(context) : context

def $enter(C) = C[ .LOCAL.FRAMES = $empty_frame :: C.LOCAL.FRAMES ]

dec $exit(context) : context

def $exit(C) = C[ .LOCAL.FRAMES = frame_t* ]
  -- if frame_h :: frame_t* = C.LOCAL.FRAMES

;;
;; Adders
;;

;;; Adder for variables

dec $add_var(cursor, context, id, varTypeIR) : context
dec $add_vars(cursor, context, id*, varTypeIR*) : context

def $add_var(GLOBAL, C, id, varTypeIR) = C'
  -- if frame = C.GLOBAL.FRAME
  -- if ~$in_set<id>(id, $dom_map<id, varTypeIR>(frame))
  -- if _ typeIR _ _ = varTypeIR
  -- if (id = "main") => $is_package_object_typeIR($canon(typeIR))
  -- if frame_update = $add_map<id, varTypeIR>(frame, id, varTypeIR)
  -- if C' = C [ .GLOBAL.FRAME = frame_update ]
def $add_var(BLOCK, C, id, varTypeIR) = C'
  -- if frame = C.BLOCK.FRAME
  -- if ~$in_set<id>(id, $dom_map<id, varTypeIR>(frame))
  -- if id =/= "main"
  -- if frame_update = $add_map<id, varTypeIR>(C.BLOCK.FRAME, id, varTypeIR)
  -- if C' = C [ .BLOCK.FRAME = frame_update ]
def $add_var(LOCAL, C, id, varTypeIR) = C'
  -- if frame_h :: frame_t* = C.LOCAL.FRAMES
  -- if ~$in_set<id>(id, $dom_map<id, varTypeIR>(frame_h))
  -- if id =/= "main"
  -- if frame_h_update = $add_map<id, varTypeIR>(frame_h, id, varTypeIR)
  -- if frame* = frame_h_update :: frame_t*
  -- if C' = C [ .LOCAL.FRAMES = frame* ]

def $add_vars(p, C, eps, eps) = C
def $add_vars(p, C, id_h :: id_t*, varTypeIR_h :: varTypeIR_t*) = C''
  -- if C' = $add_var(p, C, id_h, varTypeIR_h)
  -- if C'' = $add_vars(p, C', id_t*, varTypeIR_t*)

;;; Adder for parameters

dec $add_parameter(cursor, context, parameterTypeIR) : context
dec $add_parameters(cursor, context, parameterTypeIR*) : context

def $add_parameter(cursor, C, `EMPTY typeIR id value?) = C'
  -- if varTypeIR = `EMPTY typeIR CTK value?
  -- if C' = $add_var(cursor, C, id, varTypeIR)
def $add_parameter(cursor, C, direction typeIR id value?) = C'
  -- if direction = IN \/ direction = OUT \/ direction = INOUT
  -- if varTypeIR = direction typeIR DYN value?
  -- if C' = $add_var(cursor, C, id, varTypeIR)

def $add_parameters(cursor, C, eps) = C
def $add_parameters(cursor, C, parameterTypeIR_h :: parameterTypeIR_t*) = C''
  -- if C' = $add_parameter(cursor, C, parameterTypeIR_h)
  -- if C'' = $add_parameters(cursor, C', parameterTypeIR_t*)

;;; Adder for constructor parameters

dec $add_constructorParameter(context, parameterTypeIR) : context
dec $add_constructorParameters(context, parameterTypeIR*) : context

def $add_constructorParameter(C, direction typeIR id eps) = C'
  -- if varTypeIR = direction typeIR CTK eps
  -- if C' = $add_var(BLOCK, C, id, varTypeIR)
def $add_constructorParameter(C, direction typeIR id value) = C'
  -- if varTypeIR = direction typeIR CTK value
  -- if C' = $add_var(BLOCK, C, id, varTypeIR)

def $add_constructorParameters(C, eps) = C
def $add_constructorParameters(C, parameterTypeIR_h :: parameterTypeIR_t*) = C''
  -- if C' = $add_constructorParameter(C, parameterTypeIR_h)
  -- if C'' = $add_constructorParameters(C', parameterTypeIR_t*)

;;; Adder for types

dec $add_type(cursor, context, tid, typeDefIR) : context
def $add_type(GLOBAL, C, tid, typeDefIR) = C'
  -- if tdenv = C.GLOBAL.TDENV
  -- if ~$in_set<tid>(tid, $dom_map<tid, typeDefIR>(tdenv))
  -- if tdenv_update
      = $add_map<tid, typeDefIR>(tdenv, tid, typeDefIR)
  -- if C' = C [ .GLOBAL.TDENV = tdenv_update ]
def $add_type(BLOCK, C, tid, typeDefIR) = C'
  -- if tdenv = C.BLOCK.TDENV
  -- if ~$in_set<tid>(tid, $dom_map<tid, typeDefIR>(tdenv))
  -- if tdenv_update
      = $add_map<tid, typeDefIR>(tdenv, tid, typeDefIR)
  -- if C' = C [ .BLOCK.TDENV = tdenv_update ]
def $add_type(LOCAL, C, tid, typeDefIR) = C'
  -- if tdenv = C.LOCAL.TDENV
  -- if ~$in_set<tid>(tid, $dom_map<tid, typeDefIR>(tdenv))
  -- if tdenv_update
      = $add_map<tid, typeDefIR>(tdenv, tid, typeDefIR)
  -- if C' = C [ .LOCAL.TDENV = tdenv_update ]

dec $add_types(cursor, context, tid*, typeDefIR*) : context
def $add_types(p, C, eps, eps) = C
def $add_types(p, C, tid_h :: tid_t*, typeDefIR_h :: typeDefIR_t*) = C''
  -- if C' = $add_type(p, C, tid_h, typeDefIR_h)
  -- if C'' = $add_types(p, C', tid_t*, typeDefIR_t*)

;;; Adder for routines

dec $add_routine_overload(cursor, context, rid, routineTypeDefIR) : context

def $add_routine_overload(GLOBAL, C, rid, routineTypeDefIR) = C'
  -- if rdenv = C.GLOBAL.RDENV
  -- if ~$in_set<rid>(rid, $dom_map<rid, routineTypeDefIR>(rdenv))
  -- if rdenv_update
        = $add_map<rid, routineTypeDefIR>(rdenv, rid, routineTypeDefIR)
  -- if C' = C [ .GLOBAL.RDENV = rdenv_update ]
def $add_routine_overload(BLOCK, C, rid, routineTypeDefIR) = C'
  -- if rdenv = C.BLOCK.RDENV
  -- if ~$in_set<rid>(rid, $dom_map<rid, routineTypeDefIR>(rdenv))
  -- if rdenv_update
        = $add_map<rid, routineTypeDefIR>(rdenv, rid, routineTypeDefIR)
  -- if C' = C [ .BLOCK.RDENV = rdenv_update ]

dec $add_routine_non_overload(cursor, context, rid, routineTypeDefIR) : context

def $add_routine_non_overload(GLOBAL, C, rid, routineTypeDefIR) = C'
  -- if rdenv = C.GLOBAL.RDENV
  -- if id `( _ ) = rid
  -- if `{ (id_k `( _ ))* } = $dom_map<rid, routineTypeDefIR>(rdenv)
  -- if ~$in_set<id>(id, `{ id_k* })
  -- if rdenv_update
        = $add_map<rid, routineTypeDefIR>(rdenv, rid, routineTypeDefIR)
  -- if C' = C [ .GLOBAL.RDENV = rdenv_update ]
def $add_routine_non_overload(BLOCK, C, rid, routineTypeDefIR) = C'
  -- if rdenv = C.BLOCK.RDENV
  -- if id `( _ ) = rid
  -- if `{ (id_k `( _ ))* } = $dom_map<rid, routineTypeDefIR>(rdenv)
  -- if ~$in_set<id>(id, `{ id_k* })
  -- if rdenv_update
        = $add_map<rid, routineTypeDefIR>(rdenv, rid, routineTypeDefIR)
  -- if C' = C [ .BLOCK.RDENV = rdenv_update ]

;;; Adder for constructors

dec $add_constructor(context, cid, constructorTypeDefIR) : context

def $add_constructor(C, cid, constructorTypeDefIR) = C'
  -- if cdenv = C.GLOBAL.CDENV
  -- if ~$in_set<cid>(cid, $dom_map<cid, constructorTypeDefIR>(cdenv))
  -- if cdenv_update
        = $add_map<cid, constructorTypeDefIR>(cdenv, cid, constructorTypeDefIR)
  -- if C' = C [ .GLOBAL.CDENV = cdenv_update ]

;;
;; Finders
;;

;;; Finder for variables

dec $find_var(cursor, context, prefixedNameIR) : varTypeIR?

def $find_var(p, C, `. id) = $find_map<id, varTypeIR>(frame, id)
  -- if frame = C.GLOBAL.FRAME
def $find_var(GLOBAL, C, `` id) = $find_map<id, varTypeIR>(frame, id)
  -- if frame = C.GLOBAL.FRAME
def $find_var(BLOCK, C, `` id) = varTypeIR
  -- if frame = C.BLOCK.FRAME
  -- if varTypeIR = $find_map<id, varTypeIR>(frame, id)
def $find_var(BLOCK, C, `` id) = $find_var(GLOBAL, C, `` id)
  -- if frame = C.BLOCK.FRAME
  -- if eps = $find_map<id, varTypeIR>(frame, id)
def $find_var(LOCAL, C, `` id) = varTypeIR
  -- if frame* = C.LOCAL.FRAMES
  -- if varTypeIR = $find_maps<id, varTypeIR>(frame*, id)
def $find_var(LOCAL, C, `` id) = $find_var(BLOCK, C, `` id)
  -- if frame* = C.LOCAL.FRAMES
  -- if eps = $find_maps<id, varTypeIR>(frame*, id)

;;; Finder for values

dec $find_value(cursor, context, prefixedNameIR) : value

def $find_value(p, C, `. id) = value
  -- if frame = C.GLOBAL.FRAME
  -- if _ _ _ value = $find_map<id, varTypeIR>(frame, id)
def $find_value(GLOBAL, C, `` id) = value
  -- if frame = C.GLOBAL.FRAME
  -- if _ _ _ value = $find_map<id, varTypeIR>(frame, id)
def $find_value(BLOCK, C, `` id) = value
  -- if frame = C.BLOCK.FRAME
  -- if _ _ _ value = $find_map<id, varTypeIR>(frame, id)
def $find_value(BLOCK, C, `` id) = $find_value(GLOBAL, C, `` id)
  -- if frame = C.BLOCK.FRAME
  -- if eps = $find_map<id, varTypeIR>(frame, id)
def $find_value(LOCAL, C, `` id) = value
  -- if frame* = C.LOCAL.FRAMES
  -- if _ _ _ value = $find_maps<id, varTypeIR>(frame*, id)
def $find_value(LOCAL, C, `` id) = $find_value(BLOCK, C, `` id)
  -- if frame* = C.LOCAL.FRAMES
  -- if eps = $find_maps<id, varTypeIR>(frame*, id)

;;; Finder for types

dec $find_type(cursor, context, prefixedNameIR) : typeDefIR?

def $find_type(p, C, `. tid)
  = $find_map<tid, typeDefIR>(tdenv, tid)
  -- if tdenv = C.GLOBAL.TDENV
def $find_type(GLOBAL, C, `` tid)
  = $find_map<tid, typeDefIR>(tdenv, tid)
  -- if tdenv = C.GLOBAL.TDENV
def $find_type(BLOCK, C, `` tid) = typeDefIR
  -- if tdenv = C.BLOCK.TDENV
  -- if typeDefIR = $find_map<tid, typeDefIR>(tdenv, tid)
def $find_type(BLOCK, C, `` tid) = $find_type(GLOBAL, C, `` tid)
  -- if tdenv = C.BLOCK.TDENV
  -- if eps = $find_map<tid, typeDefIR>(tdenv, tid)
def $find_type(LOCAL, C, `` tid) = typeDefIR
  -- if tdenv* = C.LOCAL.TDENV
  -- if typeDefIR = $find_maps<tid, typeDefIR>(tdenv*, tid)
def $find_type(LOCAL, C, `` tid) = $find_type(BLOCK, C, `` tid)
  -- if tdenv* = C.LOCAL.TDENV
  -- if eps = $find_maps<tid, typeDefIR>(tdenv*, tid)

;;; Finder for routines

dec $ids_arguments(argumentIR*) : (id?)*
dec $id_argument(argumentIR) : id?

def $ids_arguments(argumentIR*) = ($id_argument(argumentIR))*
def $id_argument(typedExpressionIR) = eps
def $id_argument(nameIR `= _) = nameIR
def $id_argument(nameIR `= `_) = nameIR
def $id_argument(`_) = eps

dec $find_routine_overloaded(cursor, context, prefixedNameIR, argumentIR*)
  : (rid, routineTypeDefIR, id*)?

def $find_routine_overloaded(GLOBAL, C, `. id, argumentIR*)
  = $find_overloaded<routineTypeDefIR>(rdenv, id, (id_arg?)*)
  -- if rdenv = C.GLOBAL.RDENV
  -- if (id_arg?)* = $ids_arguments(argumentIR*)
def $find_routine_overloaded(GLOBAL, C, `` id, argumentIR*)
  = $find_overloaded<routineTypeDefIR>(rdenv, id, (id_arg?)*)
  -- if rdenv = C.GLOBAL.RDENV
  -- if (id_arg?)* = $ids_arguments(argumentIR*)
def $find_routine_overloaded(BLOCK, C, `` id, argumentIR*)
  = (rid, routineTypeDefIR, id_default*)
  -- if rdenv = C.BLOCK.RDENV
  -- if (id_arg?)* = $ids_arguments(argumentIR*)
  -- if (rid, routineTypeDefIR, id_default*)
      = $find_overloaded<routineTypeDefIR>(rdenv, id, (id_arg?)*)
def $find_routine_overloaded(BLOCK, C, `` id, argumentIR*)
  = $find_routine_overloaded(GLOBAL, C, `` id, argumentIR*)
  -- if rdenv = C.BLOCK.RDENV
  -- if (id_arg?)* = $ids_arguments(argumentIR*)
  -- if eps
      = $find_overloaded<routineTypeDefIR>(rdenv, id, (id_arg?)*)
def $find_routine_overloaded(LOCAL, C, `` id, argumentIR*)
  = $find_routine_overloaded(BLOCK, C, `` id, argumentIR*) 

dec $find_routine_non_overloaded(cursor, context, prefixedNameIR)
  : routineTypeDefIR?

def $find_routine_non_overloaded(p, C, `. id)
  = $find_non_overloaded<routineTypeDefIR>(C.GLOBAL.RDENV, id)
def $find_routine_non_overloaded(GLOBAL, C, `` id)
  = $find_non_overloaded<routineTypeDefIR>(C.GLOBAL.RDENV, id)
def $find_routine_non_overloaded(BLOCK, C, `` id) = routineTypeDefIR
  -- if routineTypeDefIR
      = $find_non_overloaded<routineTypeDefIR>(C.BLOCK.RDENV, id)
def $find_routine_non_overloaded(BLOCK, C, `` id)
  = $find_routine_non_overloaded(GLOBAL, C, `` id)
  -- if eps 
      = $find_non_overloaded<routineTypeDefIR>(C.BLOCK.RDENV, id)
def $find_routine_non_overloaded(LOCAL, C, `` id)
  = $find_routine_non_overloaded(BLOCK, C, `` id)

;;; Finder for constructors

dec $find_constructor_overloaded(context, prefixedNameIR, argumentIR*)
  : (rid, constructorTypeDefIR, id*)?

def $find_constructor_overloaded(C, `. id, argumentIR*)
  = $find_overloaded<constructorTypeDefIR>(cdenv, id, (id_arg?)*)
  -- if cdenv = C.GLOBAL.CDENV
  -- if (id_arg?)* = $ids_arguments(argumentIR*)
def $find_constructor_overloaded(C, `` id, argumentIR*)
  = $find_overloaded<constructorTypeDefIR>(cdenv, id, (id_arg?)*)
  -- if cdenv = C.GLOBAL.CDENV
  -- if (id_arg?)* = $ids_arguments(argumentIR*)

;;; Finder for return types

dec $find_return_type(context) : typeIR?

def $find_return_type(C) = typeIR_ret
  -- if FUNCTION `-> typeIR_ret = C.LOCAL.KIND
def $find_return_type(C) = typeIR_ret
  -- if EXTERN_FUNCTION `-> typeIR_ret = C.LOCAL.KIND
def $find_return_type(C) = VOID
  -- if ACTION = C.LOCAL.KIND
def $find_return_type(C) = typeIR_ret
  -- if EXTERN_METHOD `-> typeIR_ret = C.LOCAL.KIND
def $find_return_type(C) = typeIR_ret
  -- if EXTERN_METHOD ABSTRACT `-> typeIR_ret
      = C.LOCAL.KIND
def $find_return_type(C) = VOID
  -- if CONTROL_APPLY_METHOD = C.LOCAL.KIND
def $find_return_type(C) = eps
  -- otherwise

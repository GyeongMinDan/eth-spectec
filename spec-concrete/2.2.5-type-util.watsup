;;
;; Checkers for type properties
;;

;; Nominal

dec $is_nominal(typeIR) : bool
dec $is_nominal'(typeIR) : bool

def $is_nominal(typeIR) = $is_nominal'($canon(typeIR))
def $is_nominal'(TYPE _ _) = true
def $is_nominal'(STRUCT _ `{ _ }) = true
def $is_nominal'(HEADER _ `{ _ }) = true
def $is_nominal'(HEADER_UNION _ `{ _ }) = true
def $is_nominal'(ENUM _ `{ _ }) = true
def $is_nominal'(ENUM _ `# _ `{ _ }) = true
def $is_nominal'(EXTERN _ _) = true
def $is_nominal'(TABLE _ `# _) = true
def $is_nominal'(typeIR) = false
  -- otherwise

;; Default

dec $is_deftable(typeIR) : bool
dec $is_deftable'(typeIR) : bool

def $is_deftable(typeIR) = $is_deftable'($canon(typeIR))
def $is_deftable'(BOOL) = true
def $is_deftable'(ERROR) = true
def $is_deftable'(STRING) = true
def $is_deftable'(numberTypeIR) = true
def $is_deftable'(TYPE _ typeIR) = $is_deftable(typeIR)
def $is_deftable'(TUPLE `< typeIR* >) = true
  -- if $is_deftable(typeIR)*
def $is_deftable'(typeIR `[ _ ]) = $is_deftable(typeIR)
def $is_deftable'(STRUCT _ `{ (typeIR _ `;)* }) = true
  -- if $is_deftable(typeIR)*
def $is_deftable'(HEADER _ `{ (typeIR _ `;)* }) = true
  -- if $is_deftable(typeIR)*
def $is_deftable'(HEADER_UNION _ `{ (typeIR _ `;)* }) = true
  -- if $is_deftable(typeIR)*
def $is_deftable'(ENUM _ `{ _ }) = true
def $is_deftable'(ENUM _ `# typeIR `{ _ }) = $is_deftable(typeIR)
def $is_deftable'(typeIR) = false
  -- otherwise

;; Equals

dec $is_equalable(typeIR) : bool
dec $is_equalable'(typeIR) : bool

def $is_equalable(typeIR) = $is_equalable'($canon(typeIR))
def $is_equalable'(VOID) = false
def $is_equalable'(TID tid) = false
def $is_equalable'(EXTERN _ _) = false
def $is_equalable'(PARSER `( _ )) = false
def $is_equalable'(CONTROL `( _ )) = false
def $is_equalable'(PACKAGE `< _ >) = false
def $is_equalable'(TABLE _ `# _) = false
def $is_equalable'(DEFAULT) = false
def $is_equalable'(SEQ `< _ `, `... >) = false
def $is_equalable'(RECORD `{ _ `, `... }) = false
def $is_equalable'(HEADER_INVALID) = false
def $is_equalable'(SET `< _ >) = false
def $is_equalable'(TABLE_ENUM _ `{ _ }) = false
def $is_equalable'(TABLE_STRUCT _ `{ _ }) = false
def $is_equalable'(`_) = false
def $is_equalable'(typeIR) = true
  -- otherwise

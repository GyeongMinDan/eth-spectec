;;
;; Cursor
;;

syntax cursor = GLOBAL | BLOCK | LOCAL

var p : cursor

;;
;; Layered context
;;

;; Global layer

syntax glayer =
  { CDENV cdenv,
    TDENV tdenv,
    FDENV fdenv,
    FRAME frame }

;; Block layer

syntax blayer =
  { ID id,
    TDENV tdenv,
    FDENV fdenv,
    FRAME frame }

;; Local layer

syntax llayer =
  { ID id,
    TDENV tdenv,
    FRAMES frame* }

;;
;; Typing context
;;

syntax context =
  { GLOBAL glayer,
    BLOCK blayer,
    LOCAL llayer }

var C : context

;;
;; Constructor
;;

dec $empty_context : context

def $empty_context = C
  -- if glayer =
      { CDENV $empty_cdenv,
        TDENV $empty_tdenv,
        FDENV $empty_fdenv,
        FRAME $empty_frame }
  -- if blayer =
      { ID "",
        TDENV $empty_tdenv,
        FDENV $empty_fdenv,
        FRAME $empty_frame }
  -- if llayer =
      { ID "",
        TDENV $empty_tdenv,
        FRAMES ([ $empty_frame ]) }
  -- if C = { GLOBAL glayer, BLOCK blayer, LOCAL llayer }

;;
;; Scoping
;;

dec $scopeof(cursor, prefixedNonTypeName) : (cursor, name)

def $scopeof(p, nonTypeName) = (p, nonTypeName)
def $scopeof(p, `. nonTypeName) = (GLOBAL, nonTypeName)

;;
;; Bound type variabls
;;

dec $bound(cursor, context) : bound

def $bound(GLOBAL, C) = `{ eps }
def $bound(BLOCK, C) = $dom_map<tid, typeDefIR>(C.BLOCK.TDENV)
def $bound(LOCAL, C) = $union_set<tid>(bound_block, bound_local)
  -- if bound_block = $bound(BLOCK, C)
  -- if bound_local = $dom_map<tid, typeDefIR>(C.LOCAL.TDENV)

;;
;; Frame entry and exit
;;

dec $enter(context) : context

def $enter(C) = C[ .LOCAL.FRAMES = $empty_frame :: C.LOCAL.FRAMES ]

dec $exit(context) : context

def $exit(C) = C[ .LOCAL.FRAMES = frame_t* ]
  -- if frame_h :: frame_t* = C.LOCAL.FRAMES

;;
;; Return type tracking
;;

dec $return(llayer) : typeIR

;;
;; Adders
;;

;;; Adder for variables

dec $add_var(cursor, context, id, varTypeIR) : context
dec $add_vars(cursor, context, id*, varTypeIR*) : context

def $add_var(GLOBAL, C, id, varTypeIR) = C[ .GLOBAL.FRAME = frame ]
  -- if frame = $add_map<id, varTypeIR>(C.GLOBAL.FRAME, id, varTypeIR)
def $add_var(BLOCK, C, id, varTypeIR) = C[ .BLOCK.FRAME = frame ]
  -- if frame = $add_map<id, varTypeIR>(C.BLOCK.FRAME, id, varTypeIR)
def $add_var(LOCAL, C, id, varTypeIR) = C[ .LOCAL.FRAMES = frame* ]
  -- if frame_h :: frame_t* = C.LOCAL.FRAMES
  -- if frame_h_update = $add_map<id, varTypeIR>(frame_h, id, varTypeIR)
  -- if frame* = frame_h_update :: frame_t*

def $add_vars(p, C, eps, eps) = C
def $add_vars(p, C, id_h :: id_t*, varTypeIR_h :: varTypeIR_t*) = C''
  -- if C' = $add_var(p, C, id_h, varTypeIR_h)
  -- if C'' = $add_vars(p, C', id_t*, varTypeIR_t*)

;;; Adder for parameters

dec $add_param(cursor, context, paramTypeIR) : context
dec $add_params(cursor, context, paramTypeIR*) : context

def $add_param(cursor, C, id direction typeIR eps) = C'
  -- if varTypeIR = typeIR direction eps DYN
  -- if C' = $add_var(cursor, C, id, varTypeIR)
def $add_param(cursor, C, id direction typeIR val) = C'
  -- if varTypeIR = typeIR direction val DYN
  -- if C' = $add_var(cursor, C, id, varTypeIR)

def $add_params(cursor, C, eps) = C
def $add_params(cursor, C, paramTypeIR_h :: paramTypeIR_t*) = C''
  -- if C' = $add_param(cursor, C, paramTypeIR_h)
  -- if C'' = $add_params(cursor, C', paramTypeIR_t*)

;;; Adder for constructor parameters

dec $add_consParam(context, consParamTypeIR) : context
dec $add_consParams(context, consParamTypeIR*) : context

def $add_consParam(C, id direction typeIR eps) = C'
  -- if varTypeIR = typeIR direction eps CTK
  -- if C' = $add_var(BLOCK, C, id, varTypeIR)
def $add_consParam(C, id direction typeIR val) = C'
  -- if varTypeIR = typeIR direction val CTK
  -- if C' = $add_var(BLOCK, C, id, varTypeIR)

def $add_consParams(C, eps) = C
def $add_consParams(C, consParamTypeIR_h :: consParamTypeIR_t* ) = C''
  -- if C' = $add_consParam(C, consParamTypeIR_h)
  -- if C'' = $add_consParams(C', consParamTypeIR_t*)

;;; Adder for types

dec $add_type(cursor, context, tid, typeDefIR) : context
def $add_type(GLOBAL, C, tid, typeDefIR) = C'
  -- if tdenv = C.GLOBAL.TDENV
  -- if tdenv_update
        = $add_map<tid, typeDefIR>(tdenv, tid, typeDefIR)
  -- if C' = C [ .GLOBAL.TDENV = tdenv_update ]
def $add_type(BLOCK, C, tid, typeDefIR) = C'
  -- if tdenv = C.BLOCK.TDENV
  -- if tdenv_update
        = $add_map<tid, typeDefIR>(tdenv, tid, typeDefIR)
  -- if C' = C [ .BLOCK.TDENV = tdenv_update ]

;;; Adder for functions

dec $add_func(cursor, context, fid, funcTypeDefIR) : context

def $add_func(GLOBAL, C, fid, funcTypeDefIR) = C'
  -- if fdenv = C.GLOBAL.FDENV
  -- if fdenv_update
        = $add_map<fid, funcTypeDefIR>(fdenv, fid, funcTypeDefIR)
  -- if C' = C [ .GLOBAL.FDENV = fdenv_update ]
def $add_func(BLOCK, C, fid, funcTypeDefIR) = C'
  -- if fdenv = C.BLOCK.FDENV
  -- if fdenv_update
        = $add_map<fid, funcTypeDefIR>(fdenv, fid, funcTypeDefIR)
  -- if C' = C [ .BLOCK.FDENV = fdenv_update ]

;;; Adder for constructors

dec $add_cons(context, cid, consTypeDefIR) : context

def $add_cons(C, cid, consTypeDefIR) = C'
  -- if cdenv = C.GLOBAL.CDENV
  -- if cdenv_update
        = $add_map<cid, consTypeDefIR>(cdenv, cid, consTypeDefIR)
  -- if C' = C [ .GLOBAL.CDENV = cdenv_update ]

;;
;; Finders
;;

;;; Finder for variables

dec $find_var(cursor, context, id) : varTypeIR?

def $find_var(GLOBAL, C, id) = $find_map<id, varTypeIR>(frame, id)
  -- if frame = C.GLOBAL.FRAME
def $find_var(BLOCK, C, id) = varTypeIR
  -- if frame = C.BLOCK.FRAME
  -- if varTypeIR = $find_map<id, varTypeIR>(frame, id)
def $find_var(BLOCK, C, id) = $find_var(GLOBAL, C, id)
  -- if frame = C.BLOCK.FRAME
  -- if eps = $find_map<id, varTypeIR>(frame, id)
def $find_var(LOCAL, C, id) = varTypeIR
  -- if frame* = C.LOCAL.FRAMES
  -- if varTypeIR = $find_maps<id, varTypeIR>(frame*, id)
def $find_var(LOCAL, C, id) = $find_var(BLOCK, C, id)
  -- if frame* = C.LOCAL.FRAMES
  -- if eps = $find_maps<id, varTypeIR>(frame*, id)

;;; Finder for values

dec $find_val(cursor, context, id) : val?

;;; Finder for types

dec $find_type(cursor, context, tid) : typeDefIR?

def $find_type(GLOBAL, C, tid) = $find_map<tid, typeDefIR>(tdenv, tid)
  -- if tdenv = C.GLOBAL.TDENV
def $find_type(BLOCK, C, tid) = typeDefIR
  -- if tdenv = C.BLOCK.TDENV
  -- if typeDefIR = $find_map<tid, typeDefIR>(tdenv, tid)
def $find_type(BLOCK, C, tid) = $find_type(GLOBAL, C, tid)
  -- if tdenv = C.BLOCK.TDENV
  -- if eps = $find_map<tid, typeDefIR>(tdenv, tid)
def $find_type(LOCAL, C, tid) = typeDefIR
  -- if tdenv* = C.LOCAL.TDENV
  -- if typeDefIR = $find_maps<tid, typeDefIR>(tdenv*, tid)
def $find_type(LOCAL, C, tid) = $find_type(BLOCK, C, tid)
  -- if tdenv* = C.LOCAL.TDENV
  -- if eps = $find_maps<tid, typeDefIR>(tdenv*, tid)

;;; Finder for functions

dec $ids_arguments(argumentList) : (id?)*
dec $id_argument(argument) : id?

def $ids_arguments(`EMPTY) = eps
def $ids_arguments(argument) = $id_argument(argument)
def $ids_arguments(argumentList `, argument)
  = $ids_arguments(argumentList) ++ [ $id_argument(argument) ]

dec $find_func_overloaded(cursor, context, id, argumentList)
  : (fid, funcTypeDefIR, id*)?

def $find_func_overloaded(GLOBAL, C, id, argumentList)
  = $find_func<funcTypeDefIR>(fdenv, id, (id_arg?)*)
  -- if fdenv = C.GLOBAL.FDENV
  -- if (id_arg?)* = $ids_arguments(argumentList)
def $find_func_overloaded(BLOCK, C, id, argumentList)
  = (fid, funcTypeDefIR, id_default*)
  -- if fdenv = C.BLOCK.FDENV
  -- if (id_arg?)* = $ids_arguments(argumentList)
  -- if (fid, funcTypeDefIR, id_default*) = $find_func<funcTypeDefIR>(fdenv, id, (id_arg?)*)
def $find_func_overloaded(BLOCK, C, id, argumentList)
  = $find_func_overloaded(GLOBAL, C, id, argumentList)
  -- if fdenv = C.BLOCK.FDENV
  -- if (id_arg?)* = $ids_arguments(argumentList)
  -- if eps = $find_func<funcTypeDefIR>(fdenv, id, (id_arg?)*)
def $find_func_overloaded(LOCAL, C, id, argumentList)
  = $find_func_overloaded(BLOCK, C, id, argumentList) 

dec $find_func_non_overloaded(cursor, context, id) : funcTypeDefIR?

def $find_func_non_overloaded(GLOBAL, C, id) = $find_func_by_id(C.GLOBAL.FDENV, id)
def $find_func_non_overloaded(BLOCK, C, id) = funcTypeDefIR
  -- if funcTypeDefIR = $find_func_by_id(C.BLOCK.FDENV, id)
def $find_func_non_overloaded(BLOCK, C, id) = $find_func_non_overloaded(GLOBAL, C, id)
  -- if eps  = $find_func_by_id(C.BLOCK.FDENV, id)
def $find_func_non_overloaded(LOCAL, C, id) = $find_func_non_overloaded(BLOCK, C, id)

;;; Finder for constructors

dec $find_cons_overloaded(context, id, argumentList)
  : (fid, consTypeDefIR, id*)?

def $find_cons_overloaded(C, id, argumentList)
  = $find_func<consTypeDefIR>(cdenv, id, (id_arg?)*)
  -- if cdenv = C.GLOBAL.CDENV
  -- if (id_arg?)* = $ids_arguments(argumentList)

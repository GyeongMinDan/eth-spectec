;;
;; Table context
;;

syntax matchKey = (typeIR, name)

syntax matchAction = (name, paramTypeIR*, argumentIR*)

syntax matchPriority =
  { VALUES int*,
    INIT bool,
    DELTA int,
    LARGEST_WINS bool }

var prt : matchPriority

syntax matchEntry =
  { SIZE int,
    CONST bool }

var ent : matchEntry

syntax matchMode =
  | NOPRI
  | NOPRILPM nat
  | PRI
  | PRILPM

syntax tblctx =
  { KEYS matchKey*,
    ACTIONS matchAction*,
    PRIORITIES matchPriority,
    ENTRIES matchEntry,
    MODE matchMode }

;;
;; State per table entry
;;

syntax tblstate =
  | LPM nat
  | NOLPM

;;
;; Constructor
;;

dec $empty_tblctx: tblctx
def $empty_tblctx
    = { KEYS eps,
        ACTIONS eps,
        PRIORITIES matchPriority,
        ENTRIES matchEntry,
        MODE NOPRI }
  -- if matchPriority
        = { VALUES eps,
            INIT false,
            DELTA 1,
            LARGEST_WINS true }
  -- if matchEntry
        = { SIZE 0,
            CONST true }

;;
;; Adders
;;

;;; Adder for actions

dec $add_action(tblctx, name, paramTypeIR*, argumentIR*)
  : tblctx

def $add_action(tblctx, name, paramTypeIR*, argumentIR*)
  = tblctx[ .ACTIONS = tblctx.ACTIONS ++ [ matchAction ] ]
  -- if matchAction = (name, paramTypeIR*, argumentIR*)

;;
;; Finders
;;

;;; Finder for actions

dec $find_action(tblctx, name) : matchAction?
dec $find_action'(matchAction*, name) : matchAction?

def $find_action(tblctx, name)
  = $find_action'(tblctx.ACTIONS, name)

def $find_action'(eps, name) = eps

def $find_action'(matchAction_h :: matchAction_t*, name)
  = matchAction_h
  -- if (name, _, _) = matchAction_h

def $find_action'(matchAction_h :: matchAction_t*, name)
  = $find_action'(matchAction_t*, name)
  -- if (name_h, _, _) = matchAction_h
  -- if name_h =/= name

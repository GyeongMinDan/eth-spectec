;; ------------------------------------------------------------
;; get_seed
;; ------------------------------------------------------------
dec $get_seed(beaconState, epoch, domainType) : bytes32

def $get_seed(state, epoch, domainType) = bytes32_seed
  -- if $(epoch + $EPOCHS_PER_HISTORICAL_VECTOR - $MIN_SEED_LOOKAHEAD - 1) = epoch_mix_source
  -- if $get_randao_mix(state, epoch_mix_source) = bytes32_mix
  -- if $uint_to_bytes(epoch) = bytes_epoch
  -- if seedInput_pre = { DOMAIN domainType, BYTES bytes_epoch, MIX bytes32_mix }
  -- if $hash_<seedInput>(seedInput_pre) = bytes32_seed

;; ------------------------------------------------------------
;; get_beacon_proposer_index
;; ------------------------------------------------------------
dec $get_beacon_proposer_index(beaconState) : validatorIndex

def $get_beacon_proposer_index(state) = validatorIndex_prop
  -- if $get_current_epoch(state) = epoch_cur
  -- if $get_seed(state, epoch_cur, $DOMAIN_BEACON_PROPOSER) = bytes32_seed_base
  -- if $uint_to_bytes(state.SLOT) = bytes_slot
  -- if proposerSeedInput_pre = { BASE bytes32_seed_base, BYTES bytes_slot }
  -- if $hash_<proposerSeedInput>(proposerSeedInput_pre) = bytes32_seed
  -- if $get_active_validator_indices(state, epoch_cur) = validatorIndex_indices*
  -- if $compute_proposer_index(state, validatorIndex_indices*, bytes32_seed) = validatorIndex_prop

;; ------------------------------------------------------------
;; get_beacon_committee
;; ------------------------------------------------------------
dec $get_beacon_committee(beaconState, slot, committeeIndex) : validatorIndex*
def $get_beacon_committee(state, slot, committeeIndex) = validatorIndex_committee*
  -- if $compute_epoch_at_slot(slot) = epoch
  -- if $get_committee_count_per_slot(state, epoch) = uint64_committees_per_slot
  -- if $get_active_validator_indices(state, epoch) = validatorIndex_indices*
  -- if $get_seed(state, epoch, $DOMAIN_BEACON_ATTESTER) = bytes32_seed
  -- if $( (slot \ $SLOTS_PER_EPOCH) * uint64_committees_per_slot + committeeIndex ) = uint64_index
  -- if $( uint64_committees_per_slot * $SLOTS_PER_EPOCH ) = uint64_count
  -- if $compute_committee(
         validatorIndex_indices*,
         bytes32_seed,
         uint64_index,
         uint64_count
       ) = validatorIndex_committee*

;; --------------------------------------------
;; is aggregation_bits[i] true?
;; --------------------------------------------
dec $bit_is_one_at_index((nat, validatorIndex), boolean*) : bool

def $bit_is_one_at_index((n_i, validatorIndex_vi), boolean_bits*)
  = true
  -- if $(n_i < |boolean_bits*|)
  -- if boolean_bits*[n_i] = true

def $bit_is_one_at_index((n_i, validatorIndex_vi), boolean_bits*)
  = false
  -- otherwise

;; --------------------------------------------
;; validatorIndex 만 뽑기
;; --------------------------------------------
dec $project_vi((nat, validatorIndex), boolean*) : validatorIndex
def $project_vi((n_i, validatorIndex_vi), boolean_bits*) = validatorIndex_vi

;; --------------------------------------------
;; get_attesting_indices(state, attestation) : validatorIndex*
;; --------------------------------------------
dec $get_attesting_indices(beaconState, attestation) : validatorIndex*

def $get_attesting_indices(state, attestation_att) = validatorIndex_set*
  -- if $get_beacon_committee(
         state,
         attestation_att.DATA.SLOT,
         attestation_att.DATA.INDEX
       ) = validatorIndex_committee*
  -- if $enumerate_<validatorIndex>(validatorIndex_committee*)
       = (n_i, validatorIndex_vi)*
  -- if $filter_list_2_<(nat, validatorIndex), boolean*>(
         (n_i, validatorIndex_vi)*,
         attestation_att.AGGREGATION_BITS,
         def $bit_is_one_at_index
       ) = (n_sel, validatorIndex_sel)*
  -- if $map_list_2_<(nat, validatorIndex), boolean*, validatorIndex>(
         (n_sel, validatorIndex_sel)*,
         attestation_att.AGGREGATION_BITS,
         def $project_vi
       ) = validatorIndex_list*
  -- if $to_set_<validatorIndex>(validatorIndex_list*) = validatorIndex_set*